/* Edge Impulse ingestion SDK
 * Copyright (c) 2022 EdgeImpulse Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/* Includes ---------------------------------------------------------------- */
#include <Techin515_Lab4_inferencing.h>
#include <Adafruit_MPU6050.h>
#include <Adafruit_Sensor.h>
#include <Wire.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>

// === WiFi credentials ===
const char* ssid = "UW MPSK";
const char* password = "m3,@GUi~_j";

// === Server info ===
const char* serverUrl = "http://10.18.145.154:8080";
const char* studentId = "lic170";

// === WiFi connection ===
void setupWiFi() {
    Serial.println("Connecting to WiFi...");
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }
    Serial.println();
    Serial.println("Connected to " + String(ssid));
    Serial.print("IP address: ");
    Serial.println(WiFi.localIP());
}

// === Server send ===
void sendGestureToServer(const char* gesture, float confidence) {
    String jsonPayload = "{";
    jsonPayload += "\"student_id\":\"" + String(studentId) + "\",";
    jsonPayload += "\"gesture\":\"" + String(gesture) + "\",";
    jsonPayload += "\"confidence\":" + String(confidence);
    jsonPayload += "}";

    Serial.println("\n--- Sending Prediction to Server ---");
    Serial.println("URL: " + String(serverUrl));
    Serial.println("Payload: " + jsonPayload);

    HTTPClient http;
    http.begin(serverUrl);
    http.addHeader("Content-Type", "application/json");

    int httpResponseCode = http.POST(jsonPayload);

    Serial.print("HTTP Response code: ");
    Serial.println(httpResponseCode);

    if (httpResponseCode > 0) {
        String response = http.getString();
        Serial.println("Server response: " + response);
    } else {
        Serial.printf("Error sending POST: %s\n", http.errorToString(httpResponseCode).c_str());
    }

    http.end();
    Serial.println("--- End of Request ---\n");
}

// === Pins ===
#define RED_PIN    5
#define GREEN_PIN  4
#define BLUE_PIN   10
#define BUTTON_PIN 3

// === Config ===
#define SAMPLE_RATE_MS 10
#define FEATURE_SIZE   EI_CLASSIFIER_DSP_INPUT_FRAME_SIZE
#define FADE_STEPS     50
#define FADE_DELAY     20

Adafruit_MPU6050 mpu;
bool capturing = false;
unsigned long last_sample_time = 0;
int sample_count = 0;
float features[FEATURE_SIZE];

int raw_feature_get_data(size_t offset, size_t length, float *out_ptr) {
    memcpy(out_ptr, features + offset, length * sizeof(float));
    return 0;
}

void setup() {
    Serial.begin(115200);

    setupWiFi();

    analogWriteFrequency(RED_PIN, 5000);
    analogWriteFrequency(GREEN_PIN, 5000);
    analogWriteFrequency(BLUE_PIN, 5000);
    analogWriteResolution(RED_PIN, 8);
    analogWriteResolution(GREEN_PIN, 8);
    analogWriteResolution(BLUE_PIN, 8);

    pinMode(RED_PIN, OUTPUT);
    pinMode(GREEN_PIN, OUTPUT);
    pinMode(BLUE_PIN, OUTPUT);
    pinMode(BUTTON_PIN, INPUT_PULLUP);

    Serial.println("Initializing MPU6050...");
    if (!mpu.begin()) {
        Serial.println("Failed to find MPU6050 chip");
        while (1) delay(10);
    }
    mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
    mpu.setGyroRange(MPU6050_RANGE_500_DEG);
    mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);
    Serial.println("MPU6050 initialized. Press button to start gesture recognition.");
}

void loop() {
    if (digitalRead(BUTTON_PIN) == LOW && !capturing) {
        Serial.println("Button pressed! Starting gesture capture...");
        sample_count = 0;
        capturing = true;
        last_sample_time = millis();
        delay(200);
    }

    if (capturing) {
        capture_accelerometer_data();
    }
}

void capture_accelerometer_data() {
    if (millis() - last_sample_time >= SAMPLE_RATE_MS) {
        last_sample_time = millis();

        sensors_event_t a, g, temp;
        mpu.getEvent(&a, &g, &temp);

        if (sample_count < FEATURE_SIZE / 3) {
            int idx = sample_count * 3;
            features[idx]     = a.acceleration.x;
            features[idx + 1] = a.acceleration.y;
            features[idx + 2] = a.acceleration.z;
            sample_count++;
        }

        if (sample_count >= FEATURE_SIZE / 3) {
            capturing = false;
            Serial.println("Capture complete");
            run_inference();
        }
    }
}

void run_inference() {
    ei_impulse_result_t result = { 0 };
    signal_t features_signal;
    features_signal.total_length = FEATURE_SIZE;
    features_signal.get_data = &raw_feature_get_data;

    EI_IMPULSE_ERROR res = run_classifier(&features_signal, &result, false);
    if (res != EI_IMPULSE_OK) {
        Serial.print("Classifier failed: "); Serial.println(res);
        return;
    }

    // 打印所有概率
    for (uint16_t i = 0; i < EI_CLASSIFIER_LABEL_COUNT; i++) {
        Serial.printf("  %s: %.2f%%\n",
            ei_classifier_inferencing_categories[i],
            result.classification[i].value * 100);
    }

    float max_val = 0;
    int max_idx = -1;
    for (uint16_t i = 0; i < EI_CLASSIFIER_LABEL_COUNT; i++) {
        if (result.classification[i].value > max_val) {
            max_val = result.classification[i].value;
            max_idx = i;
        }
    }

    if (max_idx < 0) return;

    String gesture = ei_classifier_inferencing_categories[max_idx];
    gesture.trim();
    gesture.toUpperCase();
    Serial.printf("Prediction: %s (%.2f%%)\n", gesture.c_str(), max_val * 100);

    // ✅ Send to server
    sendGestureToServer(gesture.c_str(), max_val);

    // 控制灯光
    if (gesture.equals("Z")) {
        Serial.println("  → fading RED");
        fadeLED(RED_PIN);
    } else if (gesture.equals("O")) {
        Serial.println("  → fading BLUE");
        fadeLED(BLUE_PIN);
    } else if (gesture.equals("V")) {
        Serial.println("  → fading GREEN");
        fadeLED(GREEN_PIN);
    } else {
        Serial.println("  → no matching gesture");
    }
}

void fadeLED(int pin) {
    for (int i = 0; i <= FADE_STEPS; i++) {
        float bell = sin(PI * i / FADE_STEPS);
        int value = (int)(bell * 255);
        analogWrite(pin, value);
        delay(FADE_DELAY);
    }
    delay(200);
    for (int i = FADE_STEPS; i >= 0; i--) {
        float bell = sin(PI * i / FADE_STEPS);
        int value = (int)(bell * 255);
        analogWrite(pin, value);
        delay(FADE_DELAY);
    }
    analogWrite(pin, 0);
}
